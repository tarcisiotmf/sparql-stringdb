prefix orth: <http://purl.org/net/orth#> 
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix terms: <http://purl.org/dc/terms/> 
prefix obo: <http://purl.obolibrary.org/obo/>
prefix up: <http://purl.uniprot.org/core/>
prefix sio: <http://semanticscience.org/resource/> 
prefix : <http://purl.org/stringdb#> 
prefix api: <https://string-db.org/cgi/help.pl?subpage=api#>

CONSTRUCT {
?proteinURI_A a orth:Protein;              
    orth:organism  ?organism;
    skos:prefLabel ?preferredName_A;
    terms:identifier ?stringId_A;
    obo:RO_0001018 <https://string-db.org>. #contained in

?proteinURI_B a orth:Protein;              
    orth:organism  ?organism;
    skos:prefLabel ?preferredName_B;
    terms:identifier ?stringId_B;
    obo:RO_0001018 <https://string-db.org>. #contained in

?organism a orth:Organism;
    terms:identifier ?ncbiTaxonId ;
    obo:RO_0002162 ?taxon.
?taxon a up:Taxon;
    terms:identifier ?ncbiTaxonId .

 <https://string-db.org> rdf:type sio:SIO_000750. #A database.
        
?proteinURI_A :functionallyInteractsWith ?proteinURI_B .
?proteinURI_B :functionallyInteractsWith ?proteinURI_A .

?ppi  rdf:type :ProteinProteinInteraction ;
	:hasProtein ?proteinURI_A, ?proteinURI_B ;
	:hasFunctionalInteractionScore ?score_URI.
?score_URI rdf:type :Score;
	:combinedScore ?score;
	:neighborhoodScore ?nscore ;
	:fusionScore ?fscore ;
	:phylogeneticScore ?pscore;
	:coexpressionScore ?ascore ;
	:experimentalScore ?escore ;
	:databaseScore ?dscore ;	
	:textMiningScore ?tscore  .
}
WHERE {
    ?content
 api:ascore ?ascore;
 api:dscore ?dscore;
 api:escore ?escore;
 api:fscore ?fscore;
 api:ncbiTaxonId ?ncbiTaxonId;
 api:nscore ?nscore;
 api:preferredName_A ?preferredName_A ;
 api:preferredName_B ?preferredName_B;
 api:pscore ?pscore;
 api:score ?score;
 api:stringId_A ?stringId_A;
 api:stringId_B ?stringId_B.
 OPTIONAL{?content api:tscore ?tscore.}

    bind (IRI(concat("https://string-db.org/cgi/network?identifier=", ?stringId_A)) AS ?proteinURI_A)
    bind (IRI(concat("https://string-db.org/cgi/network?identifier=", ?stringId_B)) AS ?proteinURI_B)
    bind (IRI(concat(concat("https://string-db.org/cgi/network?identifiers=", ?stringId_A),concat("%0d" ,?stringId_B))) AS ?ppi)
    bind (IRI(concat(concat(concat("https://string-db.org/cgi/network?identifiers=", ?stringId_A),concat("%0d" ,?stringId_B)),"#SCORE")) AS ?score_URI)
    bind (IRI(concat("https://www.ncbi.nlm.nih.gov/Taxonomy/Browser/wwwtax.cgi?mode=Info&id=", ?ncbiTaxonId)) AS ?organism)
    bind (IRI(concat("http://purl.uniprot.org/taxonomy/", ?ncbiTaxonId)) AS ?taxon)

}